plugins {
    id("net.ltgt.errorprone") version "2.0.1"
}

apply plugin: "java"

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile project(':eureka-client')

    compile "com.amazonaws:aws-java-sdk-core:${awsVersion}"
    compile "com.amazonaws:aws-java-sdk-ec2:${awsVersion}"
    compile "com.amazonaws:aws-java-sdk-autoscaling:${awsVersion}"
    compile "com.amazonaws:aws-java-sdk-sts:${awsVersion}"
    compile "com.amazonaws:aws-java-sdk-route53:${awsVersion}"
    compile "javax.servlet:servlet-api:${servletVersion}"
    compile 'com.thoughtworks.xstream:xstream:1.4.17'
    compile 'javax.ws.rs:jsr311-api:1.1.1'

    // These dependencies are marked 'compileOnly' in the client, but we need them always on the server
    compile "com.fasterxml.jackson.dataformat:jackson-dataformat-xml:${jacksonVersion}"
    compile "com.fasterxml.woodstox:woodstox-core:${woodstoxVersion}"

    testCompile project(':eureka-test-utils')
    testCompile "junit:junit:${junit_version}"
    testCompile 'org.mortbay.jetty:jetty:6.1H.22'
    testCompile "org.mock-server:mockserver-netty:${mockserverVersion}"
    testCompile "com.jcraft:jzlib:1.1.3" // netty dependency
    testCompile "org.mockito:mockito-core:${mockitoVersion}"
    testRuntime 'org.slf4j:slf4j-simple:1.7.10'

    compileOnly "com.google.code.findbugs:jsr305:3.0.2"
    errorprone "com.google.errorprone:error_prone_core:2.3.2"
    errorproneJavac "com.google.errorprone:javac:9+181-r4173-1"

    annotationProcessor "com.uber.nullaway:nullaway:0.10.15"
    annotationProcessor "edu.ucr.cs.riple.annotator:annotator-scanner:1.3.15"

    compileOnly "com.uber.nullaway:nullaway-annotations:0.10.15"
}

def scanner_path = project.rootProject.projectDir.toString() + "/annotator-out/scanner.xml"
def nullaway_path = project.rootProject.projectDir.toString() + "/annotator-out/nullaway.xml"

import net.ltgt.gradle.errorprone.CheckSeverity
tasks.withType(JavaCompile) {
    // remove the if condition if you want to run NullAway on test code
    if (!name.toLowerCase().contains("test")) {
        options.errorprone.disableAllChecks = true
        options.errorprone.disableAllWarnings = true
        options.errorprone {
            check("NullAway", CheckSeverity.ERROR)
            check("AnnotatorScanner", CheckSeverity.ERROR)
            option("NullAway:AnnotatedPackages", "com.netflix.eureka")
            option("NullAway:SerializeFixMetadata", "true")
            option("NullAway:FixSerializationConfigPath", nullaway_path)
            option("AnnotatorScanner:ConfigPath", scanner_path)
        }
    }
    options.compilerArgs << "-Xmaxerrs" << "100000"
    options.compilerArgs << "-Xmaxwarns" << "100000"
}