plugins {
    id("net.ltgt.errorprone") version "2.0.1"
}

apply plugin: "java"

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation project(':eureka-client')

    implementation "com.amazonaws:aws-java-sdk-core:${awsVersion}"
    implementation "com.amazonaws:aws-java-sdk-ec2:${awsVersion}"
    implementation "com.amazonaws:aws-java-sdk-autoscaling:${awsVersion}"
    implementation "com.amazonaws:aws-java-sdk-sts:${awsVersion}"
    implementation "com.amazonaws:aws-java-sdk-route53:${awsVersion}"
    implementation "javax.servlet:servlet-api:${servletVersion}"
    implementation 'com.thoughtworks.xstream:xstream:1.4.17'
    implementation 'javax.ws.rs:jsr311-api:1.1.1'

    // These dependencies are marked 'compileOnly' in the client, but we need them always on the server
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-xml:${jacksonVersion}"
    implementation "com.fasterxml.woodstox:woodstox-core:${woodstoxVersion}"

    // https://mvnrepository.com/artifact/com.netflix.servo/servo-core
    implementation 'com.netflix.servo:servo-core:0.13.2'
    // https://mvnrepository.com/artifact/com.netflix.archaius/archaius-core
    implementation 'com.netflix.archaius:archaius-core:0.7.12'
    // https://mvnrepository.com/artifact/javax.inject/javax.inject
    implementation 'javax.inject:javax.inject:1'
    // https://mvnrepository.com/artifact/com.sun.jersey/jersey-client
    implementation 'com.sun.jersey:jersey-client:1.17'
    implementation 'org.jetbrains:annotations:24.0.0' // Replace with the latest version

    compileOnly "com.google.code.findbugs:jsr305:3.0.2"
    errorprone "com.google.errorprone:error_prone_core:2.35.1"
    errorproneJavac "com.google.errorprone:javac:9+181-r4173-1"

    annotationProcessor "com.uber.nullaway:nullaway:0.12.1"
    annotationProcessor "edu.ucr.cs.riple.annotator:annotator-scanner:1.3.15"

    compileOnly "com.uber.nullaway:nullaway-annotations:0.12.1"
}

def scanner_path = project.rootProject.projectDir.toString() + "/annotator-out/scanner.xml"
def nullaway_path = project.rootProject.projectDir.toString() + "/annotator-out/nullaway.xml"

import net.ltgt.gradle.errorprone.CheckSeverity
tasks.withType(JavaCompile) {
    // remove the if condition if you want to run NullAway on test code
    if (!name.toLowerCase().contains("test")) {
        options.errorprone.disableAllChecks = true
        options.errorprone.disableAllWarnings = true
        options.errorprone {
            check("NullAway", CheckSeverity.ERROR)
            check("AnnotatorScanner", CheckSeverity.ERROR)
            option("NullAway:AnnotatedPackages", "com.netflix.eureka")
            option("NullAway:SerializeFixMetadata", "true")
            option("NullAway:FixSerializationConfigPath", nullaway_path)
            option("AnnotatorScanner:ConfigPath", scanner_path)
        }
    }
    options.compilerArgs << "-Xmaxerrs" << "100000"
    options.compilerArgs << "-Xmaxwarns" << "100000"
}
