apply plugin: 'application'

dependencies {
    implementation project(':eureka-client')
    implementation files('conf')

    implementation "com.netflix.governator:governator:${governatorVersion}"
    runtimeOnly 'org.slf4j:slf4j-simple:1.7.7'
}

startScripts.enabled = false
run.enabled = false

task runExampleService (dependsOn: [classes], type: JavaExec) {
    group = "Run tasks"
    description = "Run the example service"

    main = "com.netflix.eureka.ExampleEurekaService"
    classpath = sourceSets.main.runtimeClasspath
    jvmArgs(["-Deureka.client.props=sample-eureka-service"])
}

task runExampleGovernatedService (dependsOn: [classes], type: JavaExec) {
    group = "Run tasks"
    description = "Run the example service"

    main = "com.netflix.eureka.ExampleEurekaGovernatedService"
    classpath = sourceSets.main.runtimeClasspath
    jvmArgs(["-Deureka.client.props=sample-eureka-service"])
}

task runExampleClient (dependsOn: [classes], type: JavaExec) {
    group = "Run tasks"
    description = "Run the example client"

    main = "com.netflix.eureka.ExampleEurekaClient"
    classpath = sourceSets.main.runtimeClasspath
    jvmArgs(["-Deureka.client.props=sample-eureka-client"])
}


applicationDistribution.into("lib/conf") {
    from("conf")
    fileMode = 0755
}
